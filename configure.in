AC_INIT(frontline/frontline.h)
PACKAGE=frontline
VERSION="0.5.3"
AM_INIT_AUTOMAKE($PACKAGE, $VERSION, no-define)
AC_DEFINE_UNQUOTED(PACKAGE, "$PACKAGE")
AC_DEFINE_UNQUOTED(VERSION, "$VERSION")
AM_MAINTAINER_MODE
AM_CONFIG_HEADER(config.h)

AC_PROG_CC
AC_PROG_CPP
AC_PROG_MAKE_SET
AM_SANITY_CHECK
AC_PROG_INSTALL
AC_PROG_AWK
AC_PROG_RANLIB

# Checks for header files.
AC_HEADER_STDC
AC_CHECK_HEADERS([errno.h stdlib.h string.h sys/time.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_MODE_T

dnl AC_ARG_PROGRAM

dnl
GNOME_INIT
ALL_LINGUAS="ja"
AM_GNU_GETTEXT
AC_CHECK_FUNC(gettext,,AC_CHECK_LIB(intl, gettext))

dnl
dnl libart2
dnl
PKG_CHECK_MODULES(LIBART2, [libart-2.0] >= 2.3.8,, 
			   AC_MSG_ERROR([Cannot find libart2]))
AC_SUBST(LIBART2_CFLAGS)
AC_SUBST(LIBART2_LIBS)

dnl
dnl Autotrace
dnl
AM_PATH_AUTOTRACE(0.31.1,, AC_MSG_ERROR([Cannot find autotrace library or the library is too old.]))

dnl
dnl GIMP
dnl
HAVE_GIMP=no
gimp_libdir=
AM_PATH_GIMP(1.2.1, HAVE_GIMP=yes, AC_MSG_WARN([*** GIMP plug-in will not be built (Cannot find gimp)]))
if test $HAVE_GIMP = yes; then
   AC_MSG_CHECKING([libgimp.*])
   gimp_libdir=`$GIMPTOOL --libdir`
   if test ! -f "${gimp_libdir}/libgimp.so" && test ! -f "${gimp_libdir}/libgimp.a"; then
      AC_MSG_RESULT(no)
      AC_MSG_WARN([*** GIMP plug-in will not be built(found gimptool but cannot find libgimp)])
      HAVE_GIMP=no
   else
      AC_MSG_RESULT(yes)
   fi
fi
AM_CONDITIONAL(HAVE_GIMP, test x$HAVE_GIMP = xyes)

dnl 
dnl popt
dnl
AC_CHECK_LIB(popt,poptPrintHelp,,AC_MSG_ERROR(*** Cannot find popt))

dnl
dnl Imlib
dnl
AM_PATH_IMLIB

AC_OUTPUT([intl/Makefile po/Makefile.in
Makefile
frontline-config
frontline.pc
frontline.spec
frontline/Makefile
gundo/Makefile
gimp/Makefile],[chmod a+x frontline-config])